## List all metabolites produced by species with near threatened IUCN status and with values available (or greater than a specific value) for trait “Seed dry mass”.


PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX emi: <https://purl.org/emi#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT DISTINCT ?ik ?wd_chem ?wdx ?spname ?tryDataLab ?tryDataVal
WHERE
    {
      { SELECT DISTINCT ?wdx ?ik ?wd_chem WHERE {
        ?material enpkg:has_lab_process ?extract ;
                ?p ?wdx . ?p rdfs:subPropertyOf enpkg:has_wd_id .
        ?p1 rdfs:subPropertyOf enpkg:has_LCMS . ?extract ?p1 ?lcms .
         ?p2 rdfs:subPropertyOf enpkg:has_lcms_feature_list . ?lcms ?p2 ?feature_list .
          ?feature_list enpkg:has_lcms_feature ?feature .
           ?p3 rdfs:subPropertyOf enpkg:has_sirius_annotation . ?feature ?p3 ?annotation .
            ?annotation enpkg:has_InChIkey2D ?ik2d .
             ?ik2d enpkg:has_smiles ?smiles ; 
                   enpkg:is_InChIkey2D_of ?ik .
              ?ik enpkg:has_wd_id ?wd_chem .
        }
      }
      {

           SERVICE <https://query.wikidata.org/sparql> {
                ?wdx wdt:P141 wd:Q719675 .
                ?wdx wdt:P225 ?spname .
            }
      }
      {
              ?trySpName emi:inTaxon ?wdx .
               ?trySpObs sosa:isSampleOf ?trySpName ;
                         sosa:isFeatureOfInterestOf ?tryObId .
              ?tryData sosa:isResultOf ?tryObId .
              ?tryData rdfs:label ?tryDataLab ; rdf:val ?tryDataVal .
                      FILTER(CONTAINS(?tryDataLab, "Seed dry mass"))
                      FILTER (?tryDataVal > 99)
      }
} LIMIT 300
